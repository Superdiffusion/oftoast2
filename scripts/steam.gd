extends Node

var tf2_exists: bool
var of_exists: bool
var sdk_exists: bool
const list_of_possible_steam_dirs = ["/.steam/steam","/.local/share/Steam","/.var/app/com.valvesoftware.Steam/data/Steam"]
var current_revision
var steam_dir
var of_dir


func get_of_path(): # return codes: 0 = steam and OF, 1 = steam but no OF, 2 = no steam
	if OS.get_name() == "X11":
		print("Linux detected!")
		var dir = Directory.new()
		var home_dir = OS.get_data_dir().split("/.local")[0]
		print(home_dir)
		for i in list_of_possible_steam_dirs:
			steam_dir = home_dir + i
			of_dir = steam_dir + "/steamapps/sourcemods/open_fortress"
			if dir.dir_exists(of_dir):
				print("Open Fortress (folder) detected on linux @ " + of_dir)
				return 0
			elif dir.dir_exists(steam_dir):
				print("Steam detected on linux, however no open fortress.")
				dir.make_dir_recursive(of_dir)
				return 0
		print("No steam install detected....?")
		return 2
	elif OS.get_name() == "Windows":
		print("this is windows!")
		var cmd_str = "@echo off\nfor /f \"tokens=2*\" %%a in ('reg query \"HKLM\\SOFTWARE\\WOW6432Node\\Valve\\Steam\" /v InstallPath') do @echo %%b"
		var temp = File.new()
		var tomp = Directory.new()
		tomp.remove("user://tmp.bat")
		temp.open("user://tmp.bat",File.WRITE)
		var bat_path = ProjectSettings.globalize_path("user://tmp.bat")
		temp.store_string(cmd_str)
		temp.close()
		var reg = []
		OS.execute("cmd.exe",["/C",bat_path],true,reg)
		if "ERROR" in reg[0]:
			print("No steam install detected....?")
			return 2
		else:
			steam_dir = reg[0].split("\n")[0].strip_edges()
			of_dir = steam_dir + "\\steamapps\\sourcemods\\open_fortress"
			var dir = Directory.new()
			if dir.dir_exists(of_dir):
				print("Open Fortress (folder) detected on windows @ " + of_dir)
				return 0
			elif dir.dir_exists(steam_dir):
				print("Steam detected on windows, however no open fortress.")
				dir.make_dir_recursive(of_dir)
				return 1 
		return 2 ## dunno what's happened to get here


func check_tf2_sdk_exists():
	var f = File.new()
	f.open(steam_dir + "/steamapps/libraryfolders.vdf",File.READ)
	var z = f.get_as_text()
	if "243750" in z:
		print("we got the sdk")
		sdk_exists = true
	if "440" in z:
		print("we got tf2")
		tf2_exists = true
			
			

func check_disk_space(path):
	if OS.get_name() == "X11":
		print("getting disk space...")
		var cmdstring = "df -PT | awk '{ if (NR!=1) {print $5, $7}}'"
		var temp = File.new()
		var tomp = Directory.new()
		tomp.remove("user://tmp.sh")
		temp.open("user://tmp.sh",File.WRITE)
		var sh_path = ProjectSettings.globalize_path("user://tmp.sh")
		temp.store_string(cmdstring)
		temp.close()
		var reg = []
		OS.execute("bash",[sh_path],true,reg)
		var a = []
		for x in reg:
			for z in x.split("\n"):
				var f = z.split(" ")
				a.append(f)
		print(a)
		var longest = [0,0]
		for x in a:
			if len(x) > 1:
				if path.begins_with(x[1]):
					if len(x[1]) > longest[0]:
						longest[0] = len(x[1])
						longest[1] = x
		if longest == [0,0]:
			return true
		if int(longest[1][0]) < 8589935: # 8gib, 1k blocks
			return false
		return true
	if OS.get_name() == "Windows":
		var cmd_str = "wmic logicaldisk get freespace,caption"
		var temp = File.new()
		var tomp = Directory.new()
		tomp.remove("user://tmp.bat")
		temp.open("user://tmp.bat",File.WRITE)
		var bat_path = ProjectSettings.globalize_path("user://tmp.bat")
		temp.store_string(cmd_str)
		temp.close()
		var reg = []
		OS.execute("cmd.exe",["/C",bat_path],true,reg)
		var a = []
		for x in reg:
			for z in x.split("\n"):
				var f = z.split(" ")
				a.append(f)
		a.pop_front() #remove caption
		for x in a:
			if path.begins_with(x[0]) and len(x) > 7:
				if int(x[7]) <= 8589934592: #bytes
					return false
				return true
